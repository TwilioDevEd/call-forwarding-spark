repositories {
    mavenCentral()
    jcenter()
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.postgresql:postgresql:9.4-1206-jdbc42'
        classpath 'org.flywaydb:flyway-gradle-plugin:3.2.1'
        classpath 'org.akhikhl.gretty:gretty:+'
    }
}

apply plugin: 'java'
apply plugin: 'org.flywaydb.flyway'
apply plugin: 'war'
apply plugin: 'org.akhikhl.gretty'

dependencies {
    compile group: 'com.sparkjava', name: 'spark-core', version:'2.5.5'
    compile group: 'com.twilio.sdk', name: 'twilio', version:'7.46.0'
    compile group: 'com.google.code.gson', name: 'gson', version:'2.3.1'
    compile group: 'org.hamcrest', name: 'hamcrest-all', version:'1.3'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.2'
    compile group: 'org.eclipse.persistence', name: 'eclipselink', version: '2.6.1'
    compile group: 'org.postgresql', name: 'postgresql', version: '9.4-1206-jdbc42'
    compile group: 'org.apache.commons', name: 'commons-io', version: '1.3.2'

    testCompile group: 'junit', name: 'junit', version:'4.11'
    testCompile 'org.mockito:mockito-core:2.7.22'
}

gretty {
    httpPort = 8080
    contextPath = '/'
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.2.1'
}

task stage {
  dependsOn assemble
}

jar.baseName = 'server'

jar {
  manifest {
    attributes 'Main-Class': 'com.twilio.callforwarding.Server'
  }
  from {
    configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
  }
}

flyway {
    user = "$System.env.DB_USERNAME"
    password = "$System.env.DB_PASSWORD"
    url = "$System.env.JDBC_URL"
}
